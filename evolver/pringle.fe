// pringle.fe
// Define a pringle shape, and let it relax

PARAMETER  RMAX = 1.0
PARAMETER  ZMAX = 0.2

boundary 1 parameters 1     //  the pringle
x1:  RMAX * cos(p1)
x2:  RMAX * sin(p1)
x3:  ZMAX * sin(2*p1)


vertices   // given in terms of boundary parameter
1    0.00  boundary 1   fixed
2    pi/3  boundary 1   fixed
3  2*pi/3  boundary 1   fixed
4    pi    boundary 1   fixed
5  4*pi/3  boundary 1   fixed
6  5*pi/3  boundary 1   fixed
7    0 0 0

edges
1    1  2  boundary 1   fixed
2    2  3  boundary 1   fixed
3    3  4  boundary 1   fixed
4    4  5  boundary 1   fixed
5    5  6  boundary 1   fixed
6    6  1  boundary 1   fixed
7    1  7
8    2  7
9    3  7 
10   4  7
11   5  7
12   6  7

faces
1   1  8  -7
2   2  9  -8
3   3 10  -9
4   4 11 -10
5   5 12 -11
6   6  7 -12

read

// Evolution to collapse and pop neck, as in Manual tutorial
gogo := { r; r; g 20; zmax := 1; g 100}
